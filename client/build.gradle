apply plugin: 'java'

repositories {
    mavenCentral()
}

def gdxVersion = "1.9.6"

dependencies {
    compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    runtime "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    runtime "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"

    compile 'io.netty:netty-all:4.1+'
    compile 'org.slf4j:slf4j-api:1.7+'
    compile 'org.slf4j:slf4j-log4j12:1.7+'
    compile 'com.google.code.gson:gson:2.8+'
    compile 'args4j:args4j:2+'
}

jar {
    manifest {
        def manifestClasspath = configurations.runtime.collect{ "./libs/"+it.getName() }.join(' ')
        attributes (
                'Implementation-Title': 'Origin game client',
                'Implementation-URL'  : 'http://www.origin-world.com/',
                'Main-Class'          : 'com.a2client.Main',
                'Class-Path'          : manifestClasspath
        )
    }
}

task zip(type: Zip, dependsOn: build) {
    from('dist')
    into('assets') {
        from('assets')
    }
    from(jar)
    into('libs') {
        from(configurations.runtime)
    }

    def fileName = 'Origin_Client_' + new Date().format("yyyy-MM-dd")
    baseName = fileName
    println 'Build in build/distributions/' + fileName + '.zip'
}

build.finalizedBy(zip)

task "rc" (type:JavaExec, dependsOn:[classes]) {
    main = 'com.a2client.Main'
    classpath = sourceSets.main.runtimeClasspath
    args = ['-s', 'localhost', '-p', '2040', '-q', '-g', '127.0.0.1']
}